buildscript {
    ext.kotlin_version = '1.1.2-5'
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'co.doppl.gradle'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def baseurl = properties.hasProperty('baseurl') ?
        properties.getProperty('baseurl') :
        'https://droidcon-server.herokuapp.com/'

def releasePassword = properties.getProperty('releasePassword')
def jwLicenseKey = properties.getProperty('jwLicenseKey')

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "co.touchlab.droidcon.anon"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 101
        versionName "0.1.01"

        buildConfigField "String", "BASE_URL", "\"$baseurl\""
        resValue "string", "jw_license_key", "$jwLicenseKey"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        staging {
            storeFile file("release.jks")
            keyAlias "android"
            storePassword "$releasePassword"
            keyPassword "$releasePassword"
        }
        release {
            storeFile file("release.jks")
            keyAlias "android"
            storePassword "$releasePassword"
            keyPassword "$releasePassword"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        staging {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/org.apache.commons/commons-lang3/pom.xml'
        exclude 'META-INF/maven/org.apache.commons/commons-lang3/pom.properties'
        exclude 'jsr305_annotations/Jsr305_annotations.gwt.xml'
        // avoid "DuplicateFileException caused by having both RxJava 1.0 and 2.0 side by side
        exclude 'META-INF/rxjava.properties'

    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }
}

def supportVersion = "25.3.1"

dependencies {

    compile "com.android.support:support-v4:$supportVersion"
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.github.johnpersano:supertoasts:1.3.4.1@aar'
    compile 'com.nineoldandroids:library:2.4.0'

    compile 'commons-io:commons-io:1.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'com.wnafee:vector-compat:1.0.5'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile "com.android.support:palette-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:customtabs:$supportVersion"

    annotationProcessor 'co.touchlab.squeaky:squeaky-processor:0.5.2'
    compile 'co.touchlab.squeaky:squeaky-query:0.5.2'

    compile 'co.touchlab:magicthreads:0.9.2'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.google.code.findbugs:jsr305:3.0.2'

    compile 'io.reactivex:rxjava:1.3.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile "com.github.akarnokd:rxjava2-interop:0.10.1"

    doppl "co.doppl.lib:androidbase:0.7.5"
    doppl 'co.doppl.com.squareup.retrofit:retrofit:1.9.0.3'
    doppl 'co.doppl.com.google.code.gson:gson:2.6.2.1'
    doppl 'co.doppl.de.greenrobot:eventbus:2.4.0.3'
    doppl 'co.touchlab.squeaky:squeaky-query:0.5.2.0'
    doppl 'co.touchlab:magicthreads:0.9.2.0'

}


dopplConfig {
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.data', 'DCD'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.presenter', 'DCP'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.tasks', 'DCT'

    copyMainOutput "../ios/dcframework/generated"
    copyDependencies true

    translatePattern {
        include '**/shared/**'
        include '**/CrashReport.*'
    }
}