buildscript {
    ext.kotlin_version = '1.1.3-2'
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath 'me.tatarka:gradle-retrolambda:3.6.1'
        classpath 'com.google.gms:google-services:3.1.0'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'co.doppl.gradle'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def baseurl = properties.hasProperty('baseurl') ? properties.getProperty('baseurl') : 'https://droidcon-server.herokuapp.com/'
def amazonurl = properties.getProperty('amazonurl') ? properties.getProperty('amazonurl') : 'https://s3.amazonaws.com/'
def releasePassword = properties.getProperty('releasePassword')

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "co.touchlab.droidcon.sf2017"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 107
        versionName "1.0.7"

        buildConfigField "String", "BASE_URL", "\"$baseurl\""
        buildConfigField "String", "AMAZON_URL", "\"$amazonurl\""
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        staging {
            storeFile file("release.jks")
            keyAlias "android"
            storePassword "$releasePassword"
            keyPassword "$releasePassword"
        }
        release {
            storeFile file("release.jks")
            keyAlias "key0"
            storePassword "$releasePassword"
            keyPassword "$releasePassword"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            pseudoLocalesEnabled true
        }
        staging {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/org.apache.commons/commons-lang3/pom.xml'
        exclude 'META-INF/maven/org.apache.commons/commons-lang3/pom.properties'
        exclude 'jsr305_annotations/Jsr305_annotations.gwt.xml'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }
}

def supportVersion = "26.1.0"
def archVer = "1.0.0-rc1"
def dopplArchVer = "1.0.0.0-rc1"
def daggerVer = "2.5"

dependencies {

    //UI Dependencies
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:palette-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:customtabs:$supportVersion"

    compile "android.arch.lifecycle:runtime:1.0.3"
    compile "android.arch.lifecycle:extensions:$archVer"


    doppl "co.doppl.android.arch.persistence.room:runtime:$dopplArchVer"
    doppl "co.doppl.android.arch.persistence.room:rxjava2:$dopplArchVer"
    doppl "co.doppl.android.arch.lifecycle:runtime:$dopplArchVer"
    doppl "co.doppl.android.arch.lifecycle:extensions:$dopplArchVer"

    annotationProcessor "android.arch.lifecycle:compiler:$archVer"
    compile "android.arch.persistence.room:runtime:$archVer"
    compile "android.arch.persistence.room:rxjava2:$archVer"
    annotationProcessor "android.arch.persistence.room:compiler:$archVer"

    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'

    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.squareup.picasso:picasso:2.4.0'

    //Shared code dependencies
    doppl 'co.doppl.lib:androidbase:0.8.5.0'
    testCompile 'co.doppl.lib:androidbasetest:0.8.5'
    testDoppl 'co.doppl.lib:androidbasetest:0.8.5.0'

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    doppl 'co.doppl.com.squareup.retrofit2.urlsession:retrofit:2.3.0.7'
    doppl 'co.doppl.com.squareup.retrofit2.urlsession:converter-gson:2.3.0.7'
    doppl 'co.doppl.com.squareup.retrofit2.urlsession:adapter-rxjava2:2.3.0.7'

    compile "io.reactivex.rxjava2:rxjava:2.1.5"
    doppl "co.doppl.io.reactivex.rxjava2:rxjava:2.1.5.0"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    doppl "co.doppl.io.reactivex.rxjava2:rxandroid:2.0.1.4"

    compile "com.google.dagger:dagger:$daggerVer"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVer"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$daggerVer"
    provided 'javax.annotation:jsr250-api:1.0'
    doppl 'co.doppl.com.google.dagger:dagger:2.5.6'

    compile 'com.google.firebase:firebase-messaging:11.4.2'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile "org.robolectric:robolectric:3.3.2"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
}


dopplConfig {
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.dagger', 'DDAG'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.data', 'DDAT'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.data.dao', 'DDAO'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.interactors', 'DINT'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.ios', 'DIOS'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.network', 'DNET'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.network.dao', 'DNETD'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.viewmodel', 'DVM'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.utils', 'DUT'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared', 'D'

    testIdentifier {
        include "**/*Test.java"
        exclude "**/OneTest.java"
    }

    translatePattern {
        include '**/shared/**'
        include '**/CrashReport.*'
        include '**/BuildConfig.*'
    }
}

apply plugin: 'com.google.gms.google-services'