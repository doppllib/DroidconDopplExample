buildscript {
    ext.kotlin_version = '1.1.2-5'
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'co.doppl.gradle'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def baseurl = properties.hasProperty('baseurl') ? properties.getProperty('baseurl') : 'https://droidcon-server.herokuapp.com/'
def amazonurl = properties.getProperty('amazonurl')
def releasePassword = properties.getProperty('releasePassword')
def jwLicenseKey = properties.getProperty('jwLicenseKey')

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "co.touchlab.droidcon.anon"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 101
        versionName "0.1.01"

        buildConfigField "String", "BASE_URL", "\"$baseurl\""
        buildConfigField "String", "AMAZON_URL", "\"$amazonurl\""
        resValue "string", "jw_license_key", "$jwLicenseKey"
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        staging {
            storeFile file("release.jks")
            keyAlias "android"
            storePassword "$releasePassword"
            keyPassword "$releasePassword"
        }
        release {
            storeFile file("release.jks")
            keyAlias "android"
            storePassword "$releasePassword"
            keyPassword "$releasePassword"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        staging {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/org.apache.commons/commons-lang3/pom.xml'
        exclude 'META-INF/maven/org.apache.commons/commons-lang3/pom.properties'
        exclude 'jsr305_annotations/Jsr305_annotations.gwt.xml'
        // avoid "DuplicateFileException caused by having both RxJava 1.0 and 2.0 side by side
        exclude 'META-INF/rxjava.properties'

    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }
}

def supportVersion = "25.3.1"
def archVer = "1.0.0-alpha1"

dependencies {

    //UI Dependencies
    compile "com.android.support:support-v4:$supportVersion"
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.github.johnpersano:supertoasts:1.3.4.1@aar'
    compile 'com.nineoldandroids:library:2.4.0'

    compile 'commons-io:commons-io:1.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile "com.android.support:palette-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:customtabs:$supportVersion"

    compile "android.arch.lifecycle:runtime:$archVer"
    compile "android.arch.lifecycle:extensions:$archVer"
    annotationProcessor "android.arch.lifecycle:compiler:$archVer"

    compile 'com.google.code.findbugs:jsr305:3.0.2'

    //Shared code dependencies
    doppl 'co.doppl.lib:androidbase:0.8.0'

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    doppl 'co.doppl.com.squareup.retrofit2.nossl:retrofit:2.3.0.0'
    doppl 'co.doppl.com.squareup.retrofit2.nossl:converter-gson:2.3.0.0'
    doppl 'co.doppl.com.squareup.retrofit2.nossl:adapter-rxjava2:2.3.0.0'

    compile "io.reactivex.rxjava2:rxjava:2.0.8"
    doppl "co.doppl.io.reactivex.rxjava2:rxjava:2.0.8.0"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile 'de.greenrobot:eventbus:2.4.0'
    doppl 'co.doppl.de.greenrobot:eventbus:2.4.0.4'

    compile 'co.touchlab:magicthreads:0.9.3'
    doppl 'co.touchlab:magicthreads:0.9.3.0'

    annotationProcessor 'co.touchlab.squeaky:squeaky-processor:0.5.3'
    compile 'co.touchlab.squeaky:squeaky-query:0.5.3'
    doppl 'co.touchlab.squeaky:squeaky-query:0.5.3.0'
    //    doppl 'co.doppl.com.google.code.gson:gson:2.6.2.1'
}


dopplConfig {
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.data', 'DCD'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.presenter', 'DCP'
    translatedPathPrefix 'co.touchlab.droidconandroid.shared.tasks', 'DCT'

    copyMainOutput "../ios/dcframework/generated"
    copyDependencies true

    translatePattern {
        include '**/shared/**'
        include '**/CrashReport.*'
    }
}